---
layout: post
title: 终于把TLS最后一章的解释器调通了
date: 2014-06-24
---

TLS看完了，但是最后一章的MINI解释器没有运行成功，心里始终有个疙瘩。昨天抽空把代码敲了一遍，终于运行成功了。而且发现了上次没有成功的原因——代码中的 apply 与系统函数命名冲突了。我一般不用DrRacket，都是在命令行下配合 emacs 使用。在命令行下 racket 的出错提示不够详细。相同的代码，在 chicken 里通过了，在 racket 里通不过，让人颇受折磨。最后抱着尝试在 DrRacket 调试，结果一运行就提示冲突，改了就好了。

为了尝试方便，我给它加了一个REPL循环，同时把 + - * / < > = 也加上去了。现在它可以做算术运算了。本来还想给它加个 define ，但是不知道要怎么搞定。

上代码+注释

```scheme
(define (repl evaluator)
  (display "repl>")
  (let ((expr (read)))
    (cond ((eq? expr 'exit)
           (display "Exiting read-eval-print loop")
           (newline))
          (else
           (write (evaluator expr))
           (newline)
           (repl evaluator)))))
  
;;辅助函数
(define first
  (lambda (l)
    (car l)))

(define second
  (lambda (l)
    (car (cdr l))))

(define third
  (lambda (l)
    (car (cdr (cdr l)))))

(define build
  (lambda (a b)
    (cons a (cons b '()))))

(define atom?
  (lambda (s)
    (and (not (pair? s))
         (not (null? s)))))

(define add1
  (lambda (n)
    (+ n 1)))

(define sub1
  (lambda (n)
    (- n 1)))
;;用嵌套列表来保存“环境”，这种嵌套的列表在TLS中称为表，每一个表由“条目(entry)“
;;构成，目也是嵌套列表，它由两个列表构成，而且两个列表的成员数量一致，前一个列表不
;;包含重复的元素，这种列表在TLS中被称为 set 。这样就构成了 名称-值 的关联。
;;下面的函数实现了通过给定第一个列表中的 名字 ，从后一个列表中找出对应的值。

(define lookup-in-entry
  (lambda (name entry entry-f)
    (lookup-in-entry-help name
                          (first entry)
                          (second entry)
                          entry-f)))

(define lookup-in-entry-help
  (lambda (name names values entry-f) ;键列表为空，或者没有匹配的情况下
    (cond                             ;交给 entry-f 去处理
     ((null? names) (entry-f name))
     ((eq? (car names) name)
      (car values))
     (else (lookup-in-entry-help name
                                 (cdr names)
                                 (cdr values)
                                 entry-f)))))

;;往 环境表 中添加条目，直接用 cons 就行，相当于给 cons 定义一个别名
(define extend-table cons)

;;向下面的函数传递一个 名字 和一个 表，历遍表中的条目返回对应的应的值
;;很显然，需要用上面的 lookup-in-entry 作为辅助函数
(define lookup-in-table
  (lambda (name table table-f)
    (cond
     ((null? table) (table-f name))
     (else
      (lookup-in-entry name
                       (car table)
                       (lambda (name)  ;这里的匿名函数对应的是上面的entry-f
                         (lookup-in-table 
                          name (cdr table) table-f)))))))


;;这里有六种基本的类型：
;;1、常量
;;2、引用
;;3、标识符
;;4、lambda
;;5、cond
;;6、程序（代入了参数的 lambda）

 ;;传入一个S-expr，根据类型选择不同的动作，首先判断是原子还是列表
(define expression-to-action
  (lambda (e)
    (cond
     ((atom? e) (atom-to-action e))
     (else (list-to-action e)))))


;;根据不同类型的原子，选择执行动作
(define atom-to-action
  (lambda (e)
    (cond
     ((number? e) *const)
     ((eq? e #t) *const)
     ((eq? e #f) *const)
     ((eq? e (quote cons)) *const)
     ((eq? e (quote car)) *const)
     ((eq? e (quote cdr)) *const)
     ((eq? e (quote null?)) *const)
     ((eq? e (quote eq?)) *const)
     ((eq? e (quote atom?)) *const)
     ((eq? e (quote zero?)) *const)
     ((eq? e (quote add1)) *const)
     ((eq? e (quote sub1)) *const)
     ((eq? e (quote number?)) *const)
     ((eq? e '+) *const)
     ((eq? e '-) *const)
     ((eq? e '*) *const)
     ((eq? e '/) *const)
     ((eq? e '>) *const)
     ((eq? e '<) *const)
     ((eq? e '=) *const)
     (else *identifier))))

;;解析列表了
(define list-to-action
  (lambda (e)
    (cond
     ((atom? (car e))
      (cond
       ((eq? (car e) (quote quote)) *quote)
       ((eq? (car e) (quote lambda)) *lambda)
       ((eq? (car e) (quote cond)) *cond) 
       (else *application)))  ;前置操作符中，除了上面的 quote lambda cond
     (else *application))))   ;统统都是过程
      ;缺少前置操作符的嵌套列表也是过程，这里表示的是接收到实参的 lambda 表达式

;;求值函数,把表达式 e 和一个空表传给下面的 meaning 函数
(define value
  (lambda (e)
    (meaning e (quote ()))))


(define meaning
  (lambda (e table)
    ((expression-to-action e) e table)))


;;expression-to-action 先对表达式 e 进行判断，返回执行动作的另一个函数
;;再把表达式 e 传入动作函数，同时传入的还家一个空列表
;;这里返回的函数就是上面提到的:
;;  *const
;;  *identifier
;;  *quote
;;  *lambda
;;  *cond
;;  *application
;; 
;;最简单的 常量 动作函数
(define *const
  (lambda (e table)
    (cond
     ((number? e) e)
     ((eq? e #t) #t)
     ((eq? e #f) #f) ;;数字和布尔值，返回它们自身
     (else
      (build (quote primitive) e)))))
;;cons car cdr null? eq? atom? zero? add1 sub1 number?
;;等等返回一个类似的列表： (primitive cons)
;;表示这是一个原始的内置过程

;;处理 quote 引用，简单地返回它后面的值
(define *quote
  (lambda (e table)
    (text-of e)))

;上面的 text-of 仅仅是 second 函数的别名
(define text-of second)


;;这里，标识符的求值用到了表了
(define *identifier
  (lambda (e table)
    (lookup-in-table e table initial-table)))

;;这里的 initial-table 就是上面缺少的 table-f 啊
(define initial-table
  (lambda (name)
    ;(car (quote ())))
    (display "Error: unbound variable: ")
    (display name)
    (newline)))


;;这里使用了错误的语法，lookup-in-table 在遇到空表的情况下
;;会触发 table-f ，从而抛出一个错误
;;问题是里 *identifier 得到的参数就是空表啊？
;;在 repl 中输入未定义的变量，确实会抛出错误，那么，
;; *identifier函数什么时候会得到含有数据的表呢？


;;处理 lambda 了
;; lambda表达式 的形参和函数体就是整个表达式的 cdr 部分
(define *lambda
  (lambda (e table)
    (build (quote non-primitive)
           (cons table (cdr e)))))

;;   (non-primitive (   (传入的参数)   (形参) (函数体) ) )
;;                         table
;;*lambda的返回值是一个这样的列表,上面的三个部分分别称为：
;;
;;  table  tormals  body
;;  
;;下面定义函数分别把它们取出来
(define table-of first)
(define formals-of second)
(define body-of third)


;;处理 cond
;(cond
;   ((cond1) 1)
;   ((cond2) 2)
;   (else 3))
;被 cdr 抽取出来，变成了：
;(
;   ((cond1) 1)
;   ((cond2) 2)
;   (else 3) )
;这就是传递给 evcon 的 lines 参数
(define evcon
  (lambda (lines table)
    (cond
     ((else? (question-of (car lines))) ;car 取出一行，用问题部分与 'else比较
      (meaning (answer-of (car lines)) table)) ;对回答部分求值
     ((meaning (question-of (car lines)) table)
      (meaning (answer-of (car lines)) table))
     (else (evcon (cdr lines) table))))) ;递归剩下的行

;;上面用到的 else?
;;用来判断是否是 else 行
(define else?
  (lambda (x)
    (cond
     ((atom? x) (eq? x (quote else)))
     (else #f))))

(define question-of first)
(define answer-of second)

;;完成 *cond 函数
(define *cond
  (lambda (e table)
    (evcon (cdr e) table)))
          ;(cdr e) 从整个表达式中去掉 cond, 再将它传给 evcon


;;处理传给函数的参数，从参数列表中取出一个参数（car），连同 table
;;一起传给 meaning 函数进行处理。
;; meaning 再交给 expression-to-action 函数判定要执行的动作
;; 简单起见，这里假定列表中的参数都是自求值的原子，于是递归 cons 的结果
;; 是 args 被原样返回了
;; 当然，args 里面可以包含更复杂的东西。引入 meaning 可以递归地处理它们
(define evlis
  (lambda (args table)
    (cond
     ((null? args) '())
     (else
      (cons (meaning (car args) table)
            (evlis (cdr args) table))))))
;;
;;(define meaning
;;  (lambda (e table)
;;    ((expression-to-action e) e table)))


;;处理过程了。这里的过程包括两种形式：
;;(+ 12 44) 这种以及 ((lambda ...) 23 43) 这种
;;不管哪一种，它们在形式上都是统一的：car 是过程，cdr 是参数
;;取出 过程 及 参数部分，再把它们交由 apply 函数进行处理
(define *application
  (lambda (e table)
    (Myapply
     (meaning (car e) table)
     (evlis (cdr e) table))))

;;首先判断是否是原始（内置）函数
(define primitive?
  (lambda (l)
    (eq? (first l) (quote primitive))))

(define non-primitive?
  (lambda (l)
    (eq? (first l) (quote non-primitive))))

;;这里传入的 fun 在前面带有 primitive 以及 non-privitive 标记
;;所以要用 second 取出函数。
;; primitive传入参数示例：
;;   (primitive cons)
;; non-primitive (lambda) 传入参数示例：
;;   (non-primitive ((table) (形参) (函数体)))
;; 经过apply变换后，lambda 表达式变成了 ((table) (形参) (函数体))
(define Myapply
  (lambda (fun vals)
    (cond
     ((primitive? fun)
      (apply-primitive (second fun) vals))
     ((non-primitive? fun)
      (apply-closure (second fun) vals)))))

;;调用同名的内置函数进行处理
(define apply-primitive
  (lambda (name vals)
    (cond
     ((eq? name 'cons)
      (cons (first vals) (second vals)))
     ((eq? name 'car)
      (car (first vals)))
     ((eq? name 'cdr)
      (cdr (first vals)))
     ((eq? name 'null?)
      (null? (first vals)))
     ((eq? name 'eq?)
      (eq? (first vals) (second vals)))
     ((eq? name 'atom?)
      (:atom? (first vals)))
     ((eq? name 'zero?)
      (zero? (first vals)))
     ((eq? name 'add1)
      (add1 (first vals)))
     ((eq? name 'sub1)
      (sub1 (first vals)))
     ((eq? name 'number?)
      (number? (first vals)))
     ((eq? name '+)
      (apply + vals))
     ((eq? name '-)
      (apply - vals))
     ((eq? name '*)
      (apply * vals))
     ((eq? name '/)
      (apply / vals))
     ((eq? name '>)
      (apply > vals))
     ((eq? name '<)
      (apply < vals))
     ((eq? name '=)
      (apply = vals)))))


(define :atom?
  (lambda (x)
    (cond
     ((atom? x) #t)
     ((null? x) #t)
     ((eq? (car x) 'primitive) #t)
     (else #f))))

;;应用非内置函数
(define apply-closure
  (lambda (closure vals)
    (meaning (body-of closure)
             (extend-table
              (new-entry (formals-of closure) vals)
              (table-of closure)))))
              
(define new-entry build)
(repl value)
```