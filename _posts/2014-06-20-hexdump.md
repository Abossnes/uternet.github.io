---
layout: post
title: 重写的 hexdump 和 encrypt
date: 2014-06-20
---

# hexdump

这是昨天用 Scheme 写的十六进制查看器的改进版。现在它与用 C 写的版本输出一致了，与 hexdump -C 的输出也是一致的了。而且改成了逐字节读取并显示，而不是把所有字节拷贝到一个列表中，性能上有提升（或者说是修复了一个大BUG，因为原来的版本读入一个巨大的文件时有可能会耗尽系统内存）。

因为 hexdump 的输出是分三列的，第一列是地址，第二列是数据，而第三列则是尝试将字节数据当成ASCII输出成字符。在模仿输出的过程中，用了太多的循环，而且用到了副作用，略蛋痛...


```scheme
#!/usr/bin/env racket
#lang racket/base

(define num->hex
  (lambda (n)
    (if (< n 16)
        (string-append "0" (number->string n 16))
        (number->string n 16))))

(define addr
  (lambda (n)
    (cond
     ((< n 256)
      (string-append "000000" (num->hex n)))
     ((< n #x1000)
      (string-append "00000" (num->hex n)))
     ((< n #x10000)
      (string-append "0000" (num->hex n)))
     ((< n #x100000)
      (string-append "000" (num->hex n)))
     ((< n #x1000000)
      (string-append "00" (num->hex n)))
     ((< n #x10000000)
      (string-append "0" (num->hex n)))
     (else (num->hex n)))))

(define ascii->char       ;;尝试把数据当成ASCII码输出字符
  (lambda (n)
    (if (and (> n 31)
             (< n 127))
        (integer->char n)
        #\.)))            ;;如果是不可打印字符，则输出 . 代替

(define print-hex
  (lambda (file)
    (let ((p (open-input-file file))
          (temp (make-vector 16))
          (counter 0))
      (let iter ((b (read-byte p))
                 (count 1))
        (if (eof-object? b) (display "")
            (begin
              (if (= (modulo (- count 1) 16) 0)  ;;打印地址
                  (begin (display (addr (- count 1))) (display "  "))
                  (display ""))
              
              (display (num->hex b)) (display " ") ;;输出十六进制数
              (vector-set! temp (modulo count 16) b) ;;将当前字节暂存在temp中
              
              (cond
               ((= (modulo count 16) 0) ;;行尾，输出ascii字符
                (begin
                  (display " |")
                  (let iter ((tc 0))
                    (if (= tc 16) (display "|\n")
                        (begin
                          (display (ascii->char (vector-ref temp tc)))
                          (iter (+ tc 1)))))))
               
               ((= (modulo count 8) 0) ;;8个字节加一个空格分列
                (display " "))
               
               (else (display "")))
              (set! counter count)     ;;这里有冗余了，我分别用了两个计数器
              (iter (read-byte p) (+ count 1)))))
      (close-input-port p) ;;关闭文件端口

      ;;补齐最后一行的行尾
      (if (not (= (modulo counter 16) 0)) 
          (let* ((puted (modulo counter 16)) ;;最后一行输出的字节
                 (blank (if (> puted 8)      ;;需要补多少个空格
                            (- (* (- 16 puted) 3) 1)
                            (* (- 16 puted) 3))))
            ;;下面三个循环分别输出空格、ASCII、空格
            (let iter ((i 0))
              (if (= i blank) (display "  |")
                  (begin
                    (display " ")
                    (iter (+ i 1)))))
            (let iter ((i 0))
              (if (= i puted) (display "")
                  (begin
                    (display (ascii->char (vector-ref temp i)))
                    (iter (+ i 1)))))
            (let iter ((i 0))
              (if (= i (- 16 puted)) (display "|\n")
                  (begin
                    (display " ")
                    (iter (+ i 1))))))
          (display "")))))   ;;racket 不允许缺少 else


(let ((f (vector-ref (current-command-line-arguments) 0)))
  (print-hex f))
```

输出示例：

```
00000000  7f 45 4c 46 01 01 01 00  00 00 00 00 00 00 00 00  |.ELF............|
00000010  02 00 03 00 01 00 00 00  00 82 04 08 34 00 00 00  |............4...|
00000020  54 06 00 00 00 00 00 00  34 00 20 00 04 00 28 00  |T.......4. ...(.|
00000030  1d 00 1c 00 03 00 00 00  b4 00 00 00 b4 80 04 08  |................|
00000040  b4 80 04 08 13 00 00 00  13 00 00 00 04 00 00 00  |................|
00000050  01 00 00 00 01 00 00 00  b4 00 00 00 b4 80 04 08  |................|
00000060  b4 80 04 08 bc 03 00 00  bc 03 00 00 05 00 00 00  |................|
00000070  00 10 00 00 01 00 00 00  80 04 00 00 80 94 04 08  |................|
00000080  80 94 04 08 c0 00 00 00  00 01 00 00 06 00 00 00  |................|
00000090  00 10 00 00 02 00 00 00  d0 04 00 00 d0 94 04 08  |................|
000000a0  d0 94 04 08 50 00 00 00  50 00 00 00 06 00 00 00  |....P...P.......|
000000b0  04 00 00 00 2f 6c 69 62  2f 6c 64 2d 6c 69 6e 75  |..../lib/ld-lin.|
000000c0  78 2e 73 6f 2e 32 00 00  00 00 00 00 00 00 00 00  |x.so.2..........|
000000d0  00 00 00 00 00 00 00 00  01 00 00 00 30 84 04 08  |............0...|
000000e0  b5 01 00 00 12 00 00 00  13 00 00 00 40 84 04 08  |............@...|
000000f0  36 00 00 00 12 00 00 00  1a 00 00 00 50 84 04 08  |6...........P...|
```

--------------

下面是最终版，增加了十六进制文本导入功能，变相地实现了一个十六进制编辑器：

首先，使用导出功能将一个二进制文件输出重定向到一个文本文件；

使用任意的文本编辑器编辑；

使用 Linux 的 cut 命令，将前面的偏移量及后面的 ASCII 文本去掉，只留下中部的十六进制文本；

最后，将编辑后的文件导入为二进制文件。

```scheme
#!/usr/bin/env racket
#lang racket/base

(define num->hex
  (lambda (n)
    (if (< n 16)
        (string-append "0" (number->string n 16))
        (number->string n 16))))

(define gen-addr
  (lambda (n)
    (cond
     ((< n 256)
      (string-append "000000" (num->hex n)))
     ((< n #x1000)
      (string-append "00000" (num->hex n)))
     ((< n #x10000)
      (string-append "0000" (num->hex n)))
     ((< n #x100000)
      (string-append "000" (num->hex n)))
     ((< n #x1000000)
      (string-append "00" (num->hex n)))
     ((< n #x10000000)
      (string-append "0" (num->hex n)))
     (else (num->hex n)))))

(define ascii->char       ;;尝试把数据当成ASCII码输出字符
  (lambda (n)
    (if (and (> n 31)
             (< n 127))
        (integer->char n)
        #\.)))            ;;如果是不可打印字符，则输出 . 代替

(define display-addr
  (lambda (n)
    (display (gen-addr n))
    (display "  ")))

(define display-byte
  (lambda (l count)
    (cond
     ((null? l) (display " "))
     (else (and (display (num->hex (car l)))
                (display " ")
                (if (= count 8)
                    (display " ")
                    (display ""))
                (display-byte (cdr l) (+ count 1)))))))
 
(define display-ascii
  (lambda (l)
    (cond ((null? l) (display ""))
          (else (and (display (ascii->char (car l)))
                     (display-ascii (cdr l)))))))

(define read-16-bytes  ;(read-16-bytes port) => (16 bytes list)
  (lambda (p)
    (let iter ((count 0))
      (if (= count 16) '()
          (let ((b (read-byte p)))
            (cond
             ((eof-object? b) '())
             (else (cons b (iter (+ count 1))))))))))

(define display-tail
  (lambda (tl)
    (let* ((puted (length tl))
           (blank (if (> puted 8)
                      (- (* (- 16 puted) 3) 1)
                      (* (- 16 puted) 3))))
      (let iter ((i 0))
        (if (= i blank) (display " |")
            (begin
              (display " ")
              (iter (+ i 1)))))
      (display-ascii tl)
      (display "|\n"))))
        
    
(define write-line-loop
  (lambda (file)
    (let ((p (open-input-file file)))
      (let iter ((count 0)
                 (l (read-16-bytes p)))
        (display-addr count)
        (display-byte l 1)  
        (cond ((< (length l) 16)
               (display-tail l))
              (else
               (display-ascii (append '(124) (append l '(124))))
               (newline)
               (iter (+ count 16) (read-16-bytes p)))))
      (close-input-port p))))

(define dump
  (lambda (inf outf)
    (let ((ip (open-input-file inf))
          (op (open-output-file outf)))
      (let iter ((char (read ip)))
        (if (eof-object? char)
            (begin
              (close-input-port ip)
              (close-output-port op))
            (begin
              (let ((str (cond
                          ((integer? char)
                           (number->string char))
                          ((symbol? char)
                           (symbol->string char)))))
                (write-byte (string->number str 16)
                            op))
              (iter (read ip))))))))

(define usage
  (lambda ()
    (printf "\n用法: hexdump <file1> [file2]\n\n")

    (printf "  只给定一个参数 file1，则将 file1 以十六进制显示
  如果给定两个参数 file1 与 file2，则将包含十六进制
  文本的 file1 以二进制存储回去

  PS.可以先以十六进制显示文件，输出重定向到另外一个文件，
  然后用任意的文本编辑器对该文件进行编辑，编辑完成后用：

  cut -b11-58 file > another_file

  去掉前面的地址偏移及后而的ASCII文本\n")
    (newline)
    (printf "Report bugs to uternet at gmail.\n")
    (exit 1)))

(let ((args (vector->list (current-command-line-arguments))))
  (cond
   ((or (null? args) (> (length args) 2))
    (usage))
   ((= 1 (length args))
    (write-line-loop (car args)))
   (else
    (dump (car args) (car (cdr args))))))
```
    

# encrypt

同样的，这个程序也改成了逐字节读取与写入。用小文件测试，性能有3倍以上的提升，大文件提升更大。

```scheme
#!/usr/bin/env racket
#lang racket/base

(define encrypt
  (lambda (n)
    (bitwise-xor n 21)))

(define rw-loop
  (lambda (inf outf)
    (let ((ip (open-input-file inf))
          (op (open-output-file outf)))
      (let iter ((byte (read-byte ip)))
        (if (eof-object? byte)
            (begin
              (close-input-port ip)
              (close-output-port op))
            (begin
              (write-byte (encrypt byte) op)
              (iter (read-byte ip))))))))

(let ((args (current-command-line-arguments)))
  (if (< (vector-length args) 2)
      (display "This program used to encrypt the file by simple XOR compute.

Usage: 
  encrypt [file1 file1]
      
file1 is the source file and the file1 is the target file.\n")
      (let*
          ((infile
            (vector-ref args 0))
           (outfile
            (vector-ref args 1)))
        (rw-loop infile outfile))))
```